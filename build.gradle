
plugins {
  id "com.github.ben-manes.versions" version "0.17.0"
}

subprojects {
  apply plugin: 'java'
  apply plugin: 'maven'
  apply plugin: 'eclipse'

  group = 'joist'
  version = [System.env.TRAVIS_TAG, 'SNAPSHOT'].find { it != null }
  archivesBaseName = "joist-${name}"

  sourceCompatibility = '1.8'
  targetCompatibility = '1.8'

  task sourcesJar(type: Jar, dependsOn: classes) {
    classifier 'sources'
    from sourceSets.main.allSource
  }

  artifacts {
    archives sourcesJar
  }

  repositories {
    mavenCentral()
    maven { url 'https://jitpack.io' }
    jcenter()
  }

  configurations {
    deployerJars
    provided
  }

  sourceSets.main.compileClasspath += configurations.provided
  sourceSets.test.runtimeClasspath += configurations.provided

  eclipse {
    classpath {
      plusConfigurations += [ configurations.provided ]
    }
  }
}

task wrapper(type: Wrapper) {
    gradleVersion = '4.10'
}

